apply plugin: "com.android.application"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'


    //配置多个productPlavor选项
    flavorDimensions "default"
    productFlavors {
        baidu{
            manifestPlaceholders = ["UMENG_CHANNEL":"baidu"]//动态替换Manifest占位符
            buildConfigField 'String',"Web_url",'"www.baidu.com"'//自定义buildConfig
            resValue 'string',"channel_des","this is baidu channel apk"//动态添加自定义资源 支持string/int/color等类型
        }
        xiaomi{
            manifestPlaceholders = ["UMENG_CHANNEL":"xiaomi"]
            buildConfigField 'String',"Web_url",'"www.xiaomi.com"'
            resValue 'tring',"channel_des","this is xiaomi channel apk"
        }
        //如果我们的Product很多 可以通一配置
        productFlavors.all{flavor->
            manifestPlaceholders = ["UMENG_CHANNEL":name]
        }
    }

    //默认的ProductFlavor配置
    defaultConfig {
        applicationId 'com.github.StormWyrm.AndroidGradleDemo2'
        minSdkVersion 19
        targetSdkVersion 28
        versionName '1.0.0'
        versionCode 1
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true //启用multiDex 开启多dex 突破对65535的限制
    }

    //编译类型配置
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            buildConfigField 'Boolean',"isDebug",'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            buildConfigField 'Boolean',"isDebug",'true'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //java编译选项
    compileOptions{
        sourceCompatibility = JavaVersion.VERSION_1_8 //配置java代码的编译版本
        targetCompatibility = JavaVersion.VERSION_1_8//配置生成java字节码的版本
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def buildTime = new Date().format("yyyyMMdd")
                outputFileName = "Example19_${variant.name}_${variant.versionName}_${buildTime}.apk"
            }

        }
    }
}
dependencies {
    implementation fileTree(dir:'libs',includes: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:multidex:1.0.1'
}
